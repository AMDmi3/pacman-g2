CMAKE_MINIMUM_REQUIRED(VERSION 2.8)
PROJECT(PACMAN-G2 C)

SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_CURRENT_SOURCE_DIR}/cmake_modules/")

include (CheckIncludeFile)
include (CheckIncludeFiles)
include (CheckFunctionExists)
include (FindPkgConfig)
include (GettextTranslate)
include (UseAsciidoc)

enable_testing()

STRING(TOLOWER ${CMAKE_PROJECT_NAME} CMAKE_PROJECT_NAME_LOWER)
SET(PACMAN_G2_VERSION 3.8.9)
SET(PACMAN_G2_LIB_VERSION ${PACMAN_G2_VERSION})
SET(PM_VERSION_TEMP "0." ${PACMAN_G2_LIB_VERSION})
STRING(REPLACE ";" "" PM_VERSION ${PM_VERSION_TEMP})

SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -pedantic -std=c99 -Wall -D_GNU_SOURCE")

OPTION(DISABLE_BINDINGS "disables all libpacman bindings" OFF)
SET(ENABLE_NLS TRUE CACHE BOOL "Translation program messages to the user's native language ?")
OPTION(BUILD_SHARED_LIBS "make a shared build" ON)
OPTION(FAKEROOT_PROOF "enable fakeroot proof protection" OFF)

IF(BUILD_SHARED_LIBS)
  SET(BUILD_TYPE SHARED)
ELSE(BUILD_SHARED_LIBS)
  SET(BUILD_TYPE STATIC)
  SET(BUILD_SHARED_LIBS OFF)
#  SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -static")
#  SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -static")
  SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -static")
#  SET(CMAKE_EXE_LINKER_FLAGS "-static -static-libgcc -pthread")
  SET(CMAKE_FIND_LIBRARY_SUFFIXES ${CMAKE_STATIC_LIBRARY_SUFFIX})
#  SET(CMAKE_FIND_LIBRARY_SUFFIXES ${CMAKE_STATIC_LIBRARY_SUFFIX} ${CMAKE_FIND_LIBRARY_SUFFIXES})
  SET(CMAKE_EXE_LINK_DYNAMIC_C_FLAGS)       # remove -Wl,-Bdynamic
  SET(CMAKE_EXE_LINK_DYNAMIC_CXX_FLAGS)
  SET(CMAKE_SHARED_LIBRARY_C_FLAGS)         # remove -fPIC
  SET(CMAKE_SHARED_LIBRARY_CXX_FLAGS)
  SET(CMAKE_SHARED_LIBRARY_LINK_C_FLAGS)    # remove -rdynamic
  SET(CMAKE_SHARED_LIBRARY_LINK_CXX_FLAGS)

#  SET(CMAKE_PROJECT_NAME_LOWER ${CMAKE_PROJECT_NAME_LOWER}.static)
  SET(CMAKE_EXECUTABLE_SUFFIX ".static")
ENDIF(BUILD_SHARED_LIBS)

SET(LIBCURL_VERSION_MIN 7.33.0)

PKG_SEARCH_MODULE(LIBARCHIVE REQUIRED libarchive)
PKG_SEARCH_MODULE(LIBCURL libpaccurl>=${LIBCURL_VERSION_MIN})
IF(LIBCURL_FOUND)
  include_directories("${LIBCURL_INCLUDEDIR}/paccurl")
ELSE(LIBCURL_FOUND)
  MESSAGE(WARNING "libpaccurl not found - trying with regular libcurl")
  PKG_SEARCH_MODULE(LIBCURL REQUIRED libcurl>=${LIBCURL_VERSION_MIN})
  INCLUDE_DIRECTORIES("${LIBCURL_INCLUDEDIR}/curl")
ENDIF(LIBCURL_FOUND)

IF(BUILD_SHARED_LIBS)
  SET(LIBPACMAN_LIBRARIES ${LIBARCHIVE_LIBRARIES} ${LIBCURL_LIBRARIES})
ELSE(BUILD_SHARED_LIBS)
  SET(LIBPACMAN_LIBRARIES ${LIBARCHIVE_STATIC_LIBRARIES} ${LIBCURL_STATIC_LIBRARIES})
ENDIF(BUILD_SHARED_LIBS)
MESSAGE(STATUS "LIBPACMAN_LIBRARIES: ${LIBPACMAN_LIBRARIES}")

FIND_PACKAGE(Asciidoc)
IF(ASCIIDOC_FOUND)
  SET(HAS_ASCIIDOC TRUE CACHE BOOL "Use Asciidoc")
ENDIF(ASCIIDOC_FOUND)

FIND_PACKAGE(Doxygen)
IF(DOXYGEN_FOUND)
  SET(HAS_DOXYGEN TRUE CACHE BOOL "Use Doxygen")
ENDIF(DOXYGEN_FOUND)

FIND_PACKAGE(Po4a)
IF(PO4A_FOUND)
  SET(HAS_PO4A TRUE CACHE BOOL "Po4a bindings")
ENDIF(PO4A_FOUND)

#needed for running the tests
FIND_PACKAGE(PythonInterp)

IF(NOT DISABLE_BINDINGS)
  FIND_PACKAGE(SWIG)
ENDIF(NOT DISABLE_BINDINGS)

IF(BUILD_SHARED_LIBS AND SWIG_FOUND)
  INCLUDE(${SWIG_USE_FILE})
  SET(CMAKE_SWIG_FLAGS "-I${CMAKE_SOURCE_DIR}/lib/libpacman/")

  FIND_PACKAGE(Mono)
  IF(MONO_FOUND)
    SET(HAS_CSHARP TRUE CACHE BOOL "C# bindings")
    ADD_SUBDIRECTORY(bindings/csharp)
  ENDIF(MONO_FOUND)

  FIND_PACKAGE(Java)
  FIND_PACKAGE(JNI)
  IF(JAVA_FOUND AND JNI_FOUND)
    SET(HAS_JAVA TRUE CACHE BOOL "Java bindings")
    ADD_SUBDIRECTORY(bindings/java)
  ENDIF(JAVA_FOUND AND JNI_FOUND)

  FIND_PACKAGE(Perl)
  IF(PERL_FOUND)
    find_package(PerlLibs REQUIRED)
    SET(HAS_PERL TRUE CACHE BOOL "Perl bindings")
    ADD_SUBDIRECTORY(bindings/perl)
  ENDIF(PERL_FOUND)

  FIND_PACKAGE(Vala)
  IF(VALA_FOUND)
    SET(HAS_VALA TRUE CACHE BOOL "Vala bindings")
    ADD_SUBDIRECTORY(bindings/vala)
  ENDIF(VALA_FOUND)

  IF(PYTHONINTERP_FOUND)
    FIND_PACKAGE(PythonLibs REQUIRED)
    SET(HAS_PYTHON TRUE CACHE BOOL "Python bindings")
    ADD_SUBDIRECTORY(bindings/python)
  ENDIF(PYTHONINTERP_FOUND)
ELSE(BUILD_SHARED_LIBS AND SWIG_FOUND)
    MESSAGE(WARNING "SWIG not found or bindings disabled, no bindings will be built")
ENDIF(BUILD_SHARED_LIBS AND SWIG_FOUND)

CHECK_INCLUDE_FILE(dlfcn.h HAVE_DLFCN_H)
CHECK_INCLUDE_FILE(inttypes.h HAVE_INTTYPES_H)
CHECK_INCLUDE_FILE(memory.h HAVE_MEMORY_H)
CHECK_INCLUDE_FILE(stdint.h HAVE_STDINT_H)
CHECK_INCLUDE_FILE(stdlib.h HAVE_STDLIB_H)
CHECK_INCLUDE_FILE(strings.h HAVE_STRINGS_H)
CHECK_INCLUDE_FILE(string.h HAVE_STRING_H)
CHECK_INCLUDE_FILE(sys/stat.h HAVE_SYS_STAT_H)
CHECK_INCLUDE_FILE(sys/types.h HAVE_SYS_TYPES_H)
CHECK_INCLUDE_FILE(unistd.h HAVE_UNISTD_H)
CHECK_INCLUDE_FILES("stdlib.h;stdarg.h;string.h;float.h" STDC_HEADERS)

CHECK_FUNCTION_EXISTS(dcgettext HAVE_DCGETTEXT)
CHECK_FUNCTION_EXISTS(gettext HAVE_GETTEXT)
CHECK_FUNCTION_EXISTS(iconv HAVE_ICONV)
CHECK_FUNCTION_EXISTS(strverscmp HAVE_STRVERSCMP)

CONFIGURE_FILE (
  "${PROJECT_SOURCE_DIR}/cmake_config.h"
  "${PROJECT_SOURCE_DIR}/config.h"
  )

ADD_SUBDIRECTORY(etc)
ADD_SUBDIRECTORY(lib/libpacman)
ADD_SUBDIRECTORY(src/pacman-g2)
ADD_SUBDIRECTORY(src/versort)
ADD_SUBDIRECTORY(src/vercmp)
ADD_SUBDIRECTORY(scripts)
ADD_SUBDIRECTORY(pactest)
